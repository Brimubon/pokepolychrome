mapscripts HoneydewTown_ProfsLab_MapScripts {

    MAP_SCRIPT_ON_FRAME_TABLE [
        VAR_HAWTH_LAB_STATE, 0: HoneydewTown_ProfsLab_EventScript_HawthIntro
    ]

}


script HoneydewTown_ProfsLab_EventScript_HawthIntro {
    lockall
    applymovement(LOCALID_PLAYER, moves(walk_up * 7))
    waitmovement(LOCALID_PLAYER)
    setspeaker(SP_NAME_HAWTHORNE)
    msgbox(HoneydewTown_ProfsLab_EventScript_HawthIntroText1, MSGBOX_AUTOCLOSE)
    delay(60)
    setspeaker(SP_NAME_HAWTHORNE)
    msgbox(HoneydewTown_ProfsLab_EventScript_HawthIntroText2, MSGBOX_AUTOCLOSE)
    applymovement(2, moves(face_left))
    waitmovement(0)
    setspeaker(SP_NAME_HAWTHORNE)
    msgbox("(W{PAUSE 48}-was that really supposed to be\n" "today...?)", MSGBOX_AUTOCLOSE)
    applymovement(2, moves(face_down))
    waitmovement(0)
    setspeaker(SP_NAME_HAWTHORNE)
    msgbox("Excuse me for a moment...", MSGBOX_AUTOCLOSE)
    applymovement(2, moves(walk_up * 2))
    waitmovement(0)
    delay(60)
    setspeaker(SP_NAME_HAWTHORNE)
    msgbox("....{PAUSE 60} .........{PAUSE 72}\n" ".................", MSGBOX_AUTOCLOSE)
    playse(SE_PIN)
    applymovement(2, moves(emote_exclamation_mark, jump_in_place_up))
    waitmovement(0)
    delay(32)
    setspeaker(SP_NAME_HAWTHORNE)
    msgbox("ACK!", MSGBOX_AUTOCLOSE)
    applymovement(2, moves(walk_down * 2))
    waitmovement(0)
    setspeaker(SP_NAME_HAWTHORNE)
    msgbox(HoneydewTown_ProfsLab_EventScript_HawthIntroText3, MSGBOX_AUTOCLOSE)
    delay(48)
    setspeaker(SP_NAME_HAWTHORNE)
    msgbox(HoneydewTown_ProfsLab_EventScript_HawthIntroText4, MSGBOX_AUTOCLOSE)
    applymovement(2, moves(walk_right))
    waitmovement(0)
    setspeaker(SP_NAME_HAWTHORNE)
    msgbox(HoneydewTown_ProfsLab_EventScript_HawthIntroText5, MSGBOX_AUTOCLOSE)
    applymovement(2, moves(walk_left, face_down))
    waitmovement(0)
    playfanfare(MUS_LEVEL_UP)
    msgbox("(A small tag was attached to your bag.)", MSGBOX_AUTOCLOSE)
    waitfanfare
    setspeaker(SP_NAME_HAWTHORNE)
    msgbox(HoneydewTown_ProfsLab_EventScript_HawthIntroText6)
    setvar(VAR_HAWTH_LAB_STATE, 1)
    releaseall
}

script HoneydewTown_ProfsLab_EventScript_Hawth {
    lock
    faceplayer
    setspeaker(SP_NAME_HAWTHORNE)
    msgbox(HoneydewTown_ProfsLab_EventScript_HawthRegText1)
    release
}

script HoneydewTown_ProfsLab_EventScript_Aide1 {
    lock
    msgbox(HoneydewTown_ProfsLab_EventScript_Aide1Text)
    release
}

script HoneydewTown_ProfsLab_EventScript_Aide2 {
    lock
    faceplayer
    msgbox(HoneydewTown_ProfsLab_EventScript_Aide2Text)
    release
}

script HoneydewTown_ProfsLab_EventScript_Hoothoot {
    lock 
    faceplayer
    playmoncry(SPECIES_HOOTHOOT, CRY_MODE_NORMAL)
    msgbox("Hoo!")
    waitmoncry
    msgbox("(It's carrying a bunch of papers in its\n" "beak.)")
    release
}

script HoneydewTown_ProfsLab_EventScript_Bookshelf {
    lock
    msgbox("(It's crammed with notes on Pokémon.\n" "...They're a little hard to read...)")
    release
}

script HoneydewTown_ProfsLab_EventScript_Machine {
    lock
    msgbox(HoneydewTown_ProfsLab_EventScript_MachineText)
    release
}

script HoneydewTown_ProfsLab_EventScript_PC {
    lock
    msgbox(HoneydewTown_ProfsLab_EventScript_PCText)
    release
}

text HoneydewTown_ProfsLab_EventScript_HawthIntroText1 {
    "......Hm?{PAUSE 72} Ah, hello there!\n"
    "Welcome to my lab!\p"
    "What can I do for you?"
}

text HoneydewTown_ProfsLab_EventScript_HawthIntroText2 {
    "...........Oh?\p"
    "So you say your name is {PLAYER} and\n"
    "you're here to receive your Pokémon?\p"
    "...I{PAUSE 48}-I see."
}

text HoneydewTown_ProfsLab_EventScript_HawthIntroText3 {
    "R-right! {PLAYER}!\n"
    "So good to finally see you in person!\p"
    "I apologize, my schedule's been rather\n"
    "hectic lately...\p"
    "About your Pokémon, well... there's a\n"
    "small problem with that.\p"
    "You see, I had a package of Poké Balls\n"
    "with Pokémon in them set to be\l"
    "delivered to Mulberry Town's Pokémon\l"
    "Center.\p"
    "I asked my niece, Scarlett, to pick it up\n"
    "for me, but...\p"
    "Well, it's been some time and she has\n"
    "yet to return, so I'm quite worried..."
}

text HoneydewTown_ProfsLab_EventScript_HawthIntroText4 {
    "Oh, I know!{PAUSE 60} {PLAYER}!\p"
    "Could you stop by Mulberry Town for me\n"
    "and make sure everything's alright?\p"
    "If there are any major problems, I want\n"
    "you and Scarlett to return here right\l"
    "away."
}

text HoneydewTown_ProfsLab_EventScript_HawthIntroText5 {
    "Now, Mulberry Town is just to the right\n"
    "of Honeydew, but you'll have to pass\l"
    "through some tall grass to get there.\p"
    "However, with this little tool right\n"
    "here, that won't be an issue!"
}

text HoneydewTown_ProfsLab_EventScript_HawthIntroText6 {
    "There! With that extra-strength\n"
    "Cleanse Tag, no wild Pokémon will\l"
    "approach you, no matter how strong.\p"
    "It's, uh, still quite fragile, though...\n"
    "so be careful!\p"
    "Well, off you go, then!"
}

text HoneydewTown_ProfsLab_EventScript_HawthRegText1 {
    "Remember, Mulberry Town is just to the\n"
    "right of here.\p"
    "With that special Cleanse Tag I gave\n"
    "you, it should be easy to reach."
}

text HoneydewTown_ProfsLab_EventScript_Aide1Text {
    "...Scyther.... Seadra........{PAUSE 60}\n"
    "...Sealeo... Seedot......\p"
    "Ugh, why can't I find our notes on\n"
    "Seaking?"
}

text HoneydewTown_ProfsLab_EventScript_Aide2Text {
    "Prof. Hawthorne studies the relations\n"
    "between different species of Pokémon.\p"
    "He used to go out a lot on fieldwork,\n"
    "but he's gotten too old for that\l"
    "nowadays.\p"
    "Now he leaves most of the fieldwork to\n"
    "his niece, Scarlett."
}

text HoneydewTown_ProfsLab_EventScript_MachineText {
    "(It's a weird looking machine.)\p"
    "(There are tons of buttons and\n" 
    "flashing lights on it... best to leave it\l"
    "alone.)"
}

text HoneydewTown_ProfsLab_EventScript_PCText {
    "(It's the Prof's PC.)\p"
    "(There's a picture of the Prof. and\n"
    "another person holding a Pokémon\l"
    "and smiling.)"
}