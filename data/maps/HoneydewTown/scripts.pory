mapscripts HoneydewTown_MapScripts {
    MAP_SCRIPT_ON_LOAD {
        setflag(FLAG_TEMP_5)
        setflag(FLAG_TEMP_6)
        setflag(FLAG_TEMP_7)
        setflag(FLAG_TEMP_8)


        random(4)
        switch(var(VAR_RESULT)) {
            case 0:
                clearflag(FLAG_TEMP_5)
            case 1:
                clearflag(FLAG_TEMP_6)
            case 2:
                clearflag(FLAG_TEMP_5)
                clearflag(FLAG_TEMP_7)
            case 3:
                clearflag(FLAG_TEMP_8)
        }
    }

    MAP_SCRIPT_ON_WARP_INTO_MAP_TABLE [
        VAR_PLYCHRME_BOAT_SPAWN, 0: HoneydewTown_EventScript_SpawnBoat
    ]

    MAP_SCRIPT_ON_FRAME_TABLE [
        VAR_PLYCHRME_INTRO, 0: HoneydewTown_EventScript_IntroCutscene
    ]

}

script HoneydewTown_EventScript_SpawnBoat {
    setvar(VAR_PLYCHRME_BOAT_SPAWN, 1)
    setobjectxyperm(LOCALID_INTRO_SHIP, 34, 32)
    addobject(LOCALID_INTRO_SHIP)
}

script HoneydewTown_EventScript_IntroCutscene {
    lockall
    removeobject(4)
    setvar(VAR_PLYCHRME_INTRO, 1)
    special(SpawnCameraObject)
    applymovement(LOCALID_INTRO_SHIP, moves(walk_fast_left * 6, walk_left * 10))
    applymovement(LOCALID_CAMERA, moves(walk_fast_left * 6, walk_left * 10))
    waitmovement(0)
    special(RemoveCameraObject)
    delay(60)
    fadescreenswapbuffers(FADE_TO_BLACK)
    delay(16)
    applymovement(LOCALID_PLAYER, moves(set_visible))
    waitmovement(0)
    playse(SE_EXIT)
    waitse
    addobject(4)
    clearflag(FLAG_HIDE_HONEYDEW_SAILOR)
    warpsilent(MAP_HONEYDEW_TOWN, 18, 31)
    waitstate()
    delay(16)
    fadescreenswapbuffers(FADE_FROM_BLACK)
    releaseall
}

script HoneydewTown_EventScript_Welcome {
    lock
    faceplayer
    msgbox(HoneydewTown_EventScript_WelcomeText)
    release
}

script HoneydewTown_EventScript_GiveMap {
    lock
    faceplayer 
    msgbox(HoneydewTown_EventScript_GiveMapText)
    release
}

script HoneydewTown_EventScript_Shoes {
    lock
    faceplayer
    msgbox(HoneydewTown_EventScript_ShoesText)
    release 
}

script HoneydewTown_EventScript_Garden {
    lock
    faceplayer
    msgbox(HoneydewTown_EventScript_GardenText)
    release
}

script HoneydewTown_EventScript_MainSign {
    lock
	msgbox(HoneydewTown_EventScript_MainSignText)
	release
}

script HoneydewTown_EventScript_ProfSign {
    lock
    msgbox(HoneydewTown_EventScript_ProfSignText)
    release
}

script HoneydewTown_EventScript_MeetHawthFirst {
    lock
    msgbox(HoneydewTown_EventScript_MeetHawthFirstText, MSGBOX_AUTOCLOSE)
    applymovement(LOCALID_PLAYER, moves(walk_left))
    waitmovement(LOCALID_PLAYER)
    release
}

text HoneydewTown_EventScript_WelcomeText {
    "We've made it to Honeydew!\n"
    "Hope the ride wasn't too rough, hehehe.\p"
    "If you're lookin' for the professor,\n"
    "he's probably in his lab, in the\l"
    "northwest part of town. Hard to miss."
}

text HoneydewTown_EventScript_GiveMapText {
    "I saw you get off that boat...\n"
    "Are you new to the Coeze region?\p"
    "Here, take this.\n"
    "Consider it a welcome present."
}

text HoneydewTown_EventScript_ShoesText {
    "Um... I like your running shoes!\n"
    "I have a pair like that, too.\p"
    "Uh... you can press B to start running,\n"
    "but you probably already knew that..."
}

text HoneydewTown_EventScript_GardenText {
    "Tending to this garden isn't easy,\n"
    "but to me, it's worth it.\p"
    "With all the people coming to Coeze for\n"
    "the League challenge, I want to make\l"
    "this town as pretty as it can be."
}

text HoneydewTown_EventScript_MainSignText {
    "Honeydew Town- \n"
    "“Facilitating new beginnings.”"
}

text HoneydewTown_EventScript_ProfSignText {
    "Prof. Hawthorne's Lab"
}

text HoneydewTown_EventScript_MeetHawthFirstText {
    "(You should probably meet with Prof.\n"
    "Hawthorne first.)\p"
    "(His lab should be somewhere around\n"
    "here...)"
}
