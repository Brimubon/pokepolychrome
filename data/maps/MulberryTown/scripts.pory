mapscripts MulberryTown_MapScripts {
    MAP_SCRIPT_ON_LOAD {
        setflag(FLAG_TEMP_5)
        setflag(FLAG_TEMP_6)
        setflag(FLAG_TEMP_7)
        setflag(FLAG_TEMP_8)

        random(4)
        switch(var(VAR_RESULT)) {
            case 0:
                clearflag(FLAG_TEMP_7)
                clearflag(FLAG_TEMP_8)
            case 1:
                clearflag(FLAG_TEMP_5)
                clearflag(FLAG_TEMP_6)
            case 2:
                clearflag(FLAG_TEMP_6)
                clearflag(FLAG_TEMP_7)
                clearflag(FLAG_TEMP_8)
            case 3:
                clearflag(FLAG_TEMP_5)
        }
    }
}

script MulberryTown_EventScript_SuspiciousGrunt {
    lockall
    playse(SE_PIN)
    applymovement(12, moves(walk_in_place_fast_right, emote_exclamation_mark))
    waitmovement(0)
    applymovement(LOCALID_PLAYER, moves(walk_in_place_left))
    waitmovement(0)
    delay(16)
    msgbox(MulberryTown_EventScript_SuspiciousGruntText1, MSGBOX_AUTOCLOSE)
    playse(SE_LEDGE)
    applymovement(LOCALID_PLAYER, moves(face_up, lock_facing_direction, jump_down, unlock_facing_direction))
    applymovement(12, moves(walk_right, face_down))
    waitmovement(0)
    msgbox("Now SCRAM!", MSGBOX_AUTOCLOSE)
    applymovement(12, moves(walk_left, face_up))
    waitmovement(0)
    releaseall
}

script MulberryTown_EventScript_Grunt {
    lock
    msgbox(MulberryTown_EventScript_GruntText)
    release
}

script MulberryTown_EventScript_Artist1 {
    lock
    faceplayer
    msgbox(MulberryTown_EventScript_Artist1Text)
    release 
}

script MulberryTown_EventScript_BoyWithDog {
    lock 
    faceplayer
    msgbox(MulberryTown_EventScript_BoyWithDogText)
    applymovement(5, moves(face_left))
    waitmovement
    release
}

script MulberryTown_EventScript_Growlithe {
    lock 
    faceplayer
    playmoncry(SPECIES_GROWLITHE, CRY_MODE_NORMAL)
    msgbox("Grr-woof!")
    waitmoncry
    applymovement(6, moves(face_right))
    waitmovement
    release
}

script MulberryTown_EventScript_OldLady {
    lock
    dotimebasedevents
    gettimeofday
    if(var(VAR_0x8000) == TIME_EVENING || var(VAR_0x8000) == TIME_NIGHT) {
        msgbox("Zzzz...\p" "(Looks like she fell asleep...)")
        release
        end
    } else {
        faceplayer
        msgbox(MulberryTown_EventScript_OldLadyAwakeText)
        release
        end
    }
}


script MulberryTown_EventScript_MainSign {
    lock
    msgbox(MulberryTown_EventScript_MainSignText)
    release 
}

script MulberryTown_EventScript_ForestSign {
    lock
    msgbox(MulberryTown_EventScript_ForestSignText)
    release
}

text MulberryTown_EventScript_SuspiciousGruntText1 {
    "Huh?{PAUSE 72} H-hey!\n"
    "You can't go in there right now!\p"
    "....What? D{PAUSE 48}-don't ask me why!\n"
    "I don't need to explain myself to you!"
}

text MulberryTown_EventScript_GruntText {
    "...Those little pests must've gone in\n"
    "here... there's nowhere else they could\l"
    "possibly be...\p"
    "I swear, once I get my hands on those..."
}

text MulberryTown_EventScript_Artist1Text {
    "Every now and then, I go into Mulberry\n"
    "Woods in search of the perfect muse.\p"
    "I hear rumors of a truly elegant\n"
    "Pokémon that resides there, but...\p"
    "I fear my own Pokémon aren't strong\n"
    "enough to go looking for it on my own.\p"
    "Maybe if... oh, never you mind."
}

text MulberryTown_EventScript_BoyWithDogText {
    "A strange Pokémon just ran through\n"
    "town and into the woods over there.\p"
    "It moved so fast, it spooked my\n"
    "Growlithe.\p"
    "Be careful if you're going in there, ok?"
}

text MulberryTown_EventScript_OldLadyAwakeText {
    "This town's atmosphere is always so\n"
    "quiet and peaceful.\p"
    "Sometimes I worry I'll fall asleep\n"
    "sitting out here like this, ohoho..."
}

text MulberryTown_EventScript_MainSignText {
    "Mulberry Town- \n"
    "“The elegant seaside town.”"
}

text MulberryTown_EventScript_ForestSignText {
    "Entering Mulberry Woods"
}
