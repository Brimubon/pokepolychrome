mapscripts MulberryWoods_MapScripts {
}

script MulberryWoods_EventScript_ScarlettEncounter0 {
    lockall
    setvar(VAR_0x8008, 0)
    goto(MulberryWoods_EventScript_Scarlett)
    end
}

script MulberryWoods_EventScript_ScarlettEncounter1 {
    lockall
    setvar(VAR_0x8008, 1)
    goto(MulberryWoods_EventScript_Scarlett)
    end
}

script MulberryWoods_EventScript_Scarlett {
    playse(SE_PIN)
    applymovement(9, moves(walk_in_place_down, emote_exclamation_mark))
    waitmovement(0)
    setspeaker(SP_NAME_SCARLETT)
    msgbox(MulberryWoods_EventScript_ScarlettText1, MSGBOX_AUTOCLOSE)
    if(var(VAR_0x8008) == 0) {
        applymovement(LOCALID_PLAYER, moves(walk_up * 3, walk_in_place_left))
        waitmovement(LOCALID_PLAYER)
    } elif (var(VAR_0x8008) == 1) {
        applymovement(LOCALID_PLAYER, moves(walk_up, walk_right, walk_up * 2, walk_in_place_left))
        waitmovement(LOCALID_PLAYER)
    }
    applymovement(9, moves(walk_in_place_right))
    waitmovement(0)
    setspeaker(SP_NAME_SCARLETT)
    msgbox(MulberryWoods_EventScript_ScarlettText2, MSGBOX_AUTOCLOSE)
    applymovement(9, moves(walk_in_place_left))
    waitmovement(0)
    setspeaker(SP_NAME_SCARLETT)
    msgbox(MulberryWoods_EventScript_ScarlettText3, MSGBOX_AUTOCLOSE)
    applymovement(9, moves(walk_in_place_right))
    waitmovement(0)
    setspeaker(SP_NAME_SCARLETT)
    msgbox(MulberryWoods_EventScript_ScarlettText4, MSGBOX_AUTOCLOSE)
    applymovement(9, moves(walk_left * 8))
    waitmovement(0)
    removeobject(9)
    setflag(FLAG_HIDE_SCARLETT_MULWOODS1)
    applymovement(LOCALID_PLAYER, moves(walk_in_place_right))
    waitmovement(0)
    setvar(VAR_MULBERRY_TOWN_GRUNT, 2)
    releaseall
}

script MulberryWoods_EventScript_SeviperEncounter0 {
    lockall
    setvar(VAR_0x8008, 0)
    goto(MulberryWoods_EventScript_SeviperEncounter)
    end
}

script MulberryWoods_EventScript_SeviperEncounter1 {
    lockall
    setvar(VAR_0x8008, 1)
    goto(MulberryWoods_EventScript_SeviperEncounter)
    end
}

script MulberryWoods_EventScript_SeviperEncounter2 {
    lockall
    setvar(VAR_0x8008, 2)
    goto(MulberryWoods_EventScript_SeviperEncounter)
    end
}

script MulberryWoods_EventScript_SeviperEncounter3 {
    lockall
    setvar(VAR_0x8008, 3)
    goto(MulberryWoods_EventScript_SeviperEncounter)
    end
}

script MulberryWoods_EventScript_SeviperEncounter {
    playmoncry(SPECIES_SEVIPER, CRY_MODE_NORMAL)
    waitmoncry
    delay(30)
    playse(SE_PIN)
    applymovement(LOCALID_PLAYER, moves(emote_question_mark))
    waitmovement(LOCALID_PLAYER)
    msgbox("...Huh?\n" "That sounded like a Pokémon!", MSGBOX_AUTOCLOSE)
    if(var(VAR_0x8008) == 0) {
        applymovement(LOCALID_PLAYER, moves(walk_right * 2, walk_up * 2))
        waitmovement(LOCALID_PLAYER)
    } elif (var(VAR_0x8008) == 1) {
        applymovement(LOCALID_PLAYER, moves(walk_right * 2, walk_up * 3))
        waitmovement(LOCALID_PLAYER)
    } elif (var(VAR_0x8008) == 2) {
        applymovement(LOCALID_PLAYER, moves(walk_right * 2, walk_up * 4))
        waitmovement(LOCALID_PLAYER)
    } elif (var(VAR_0x8008) == 3) {
        applymovement(LOCALID_PLAYER, moves(walk_right * 2, walk_up * 5))
        waitmovement(LOCALID_PLAYER)
    }
    playse(SE_PIN)
    applymovement(12, moves(face_player, emote_exclamation_mark))
    waitmovement(0)
    delay(72)
    msgbox("(There's that black tag from before.\n" "This time it's wrapped around its tail.)", MSGBOX_AUTOCLOSE)
    applymovement(LOCALID_PLAYER, moves(walk_in_place_up))
    waitmovement(LOCALID_PLAYER)
    delay(72)
    playse(SE_PIN)
    applymovement(12, moves(emote_question_mark))
    waitmovement(0)
    delay(90)
    applymovement(12, moves(walk_in_place_left))
    waitmovement(0)
    delay(60)
    msgbox("... ... ... ... ...{PAUSE 84} ... ... ... ... ... ... ...", MSGBOX_AUTOCLOSE)
    delay(48)
    msgbox("(You removed the tag.)", MSGBOX_AUTOCLOSE) 
    playse(SE_PIN)
    applymovement(12, moves(face_player, emote_exclamation_mark))
    waitmovement(0)
    delay(106)
    playse(SE_PIN)
    applymovement(12, moves(emote_heart))
    waitmovement(0)
    delay(120)
    applymovement(LOCALID_PLAYER, moves(lock_facing_direction, walk_down, unlock_facing_direction))
    waitmovement(LOCALID_PLAYER)
    applymovement(12, moves(walk_down, walk_right * 4, delay_16, face_left))
    waitmovement(0)
    applymovement(LOCALID_PLAYER, moves(walk_in_place_right))
    waitmovement(LOCALID_PLAYER)
    delay(90)
    playmoncry(SPECIES_SEVIPER, CRY_MODE_NORMAL)
    waitmoncry
    delay(60)
    applymovement(12, moves(walk_up * 2, walk_right * 6))
    waitmovement(0)
    removeobject(12)
    setvar(VAR_MET_PLYCHRME_STARTERS, 2)
    setflag(FLAG_HIDE_SEVIPER)
    delay(90)
    releaseall
}

script MulberryWoods_EventScript_GruntEncounter0 {
    lockall
    setvar(VAR_0x8008, 0)
    goto(MulberryWoods_EventScript_GruntEncounter)
    end
}

script MulberryWoods_EventScript_GruntEncounter1 {
    lockall
    setvar(VAR_0x8008, 1)
    goto(MulberryWoods_EventScript_GruntEncounter)
    end
}

script MulberryWoods_EventScript_GruntEncounter2 {
    lockall
    setvar(VAR_0x8008, 2)
    goto(MulberryWoods_EventScript_GruntEncounter)
    end
}

script MulberryWoods_EventScript_GruntEncounter {
    setvar(VAR_MET_PLYCHRME_STARTERS, 3)
    playmoncry(SPECIES_ZANGOOSE, CRY_MODE_NORMAL)
    waitmoncry
    playmoncry(SPECIES_SEVIPER, CRY_MODE_NORMAL)
    waitmoncry
    delay(48)
    playse(SE_PIN)
    applymovement(LOCALID_PLAYER, moves(emote_exclamation_mark))
    waitmovement(LOCALID_PLAYER)
    if(var(VAR_0x8008) == 0) {
        applymovement(LOCALID_PLAYER, moves(walk_up * 4, walk_left * 4))
        waitmovement(LOCALID_PLAYER)
    } elif (var(VAR_0x8008) == 1) {
        applymovement(LOCALID_PLAYER, moves(walk_up * 3, walk_left * 3, walk_up, walk_left * 2))
        waitmovement(LOCALID_PLAYER)
    } elif (var(VAR_0x8008) == 2) {
        applymovement(LOCALID_PLAYER, moves(walk_up * 3, walk_left * 4, walk_up, walk_left * 2))
        waitmovement(LOCALID_PLAYER)
    }
    delay(90)
    applymovement(13, moves(lock_facing_direction, walk_left, unlock_facing_direction))
    waitmovement(0)
    delay(60)
    playse(SE_WALL_HIT)
    applymovement(13, moves(walk_faster_right * 2))
    applymovement(14, moves(delay_16, lock_facing_direction, walk_right, unlock_facing_direction))
    waitmovement(0)
    delay(72)
    playse(SE_WALL_HIT)
    applymovement(14, moves(walk_fast_left))
    applymovement(13, moves(delay_16, lock_facing_direction, walk_left, unlock_facing_direction))
    waitmovement(0)
    delay(60)
    playmoncry(SPECIES_SEVIPER, CRY_MODE_NORMAL)
    waitmoncry
    playmoncry(SPECIES_ZANGOOSE, CRY_MODE_NORMAL)
    waitmoncry
    applymovement(LOCALID_PLAYER, moves(walk_slow_left))
    waitmovement(LOCALID_PLAYER)
    playse(SE_PIN)
    applymovement(13, moves(emote_double_exclamation_mark))
    applymovement(14, moves(face_right, emote_double_exclamation_mark))
    waitmovement(0)
    delay(90)
    playse(SE_PIN)
    applymovement(14, moves(emote_heart))
    applymovement(13, moves(emote_heart, walk_fast_right, walk_fast_down, walk_fast_right * 3, face_up))
    waitmovement(0)
    delay(60)
    applymovement(LOCALID_PLAYER, moves(face_down, delay_16 * 2, face_left))
    waitmovement(LOCALID_PLAYER)
    msgbox("(... ... ...Hm?)\p" "(These Pokémon are carrying\n" "Poké Balls...)", MSGBOX_AUTOCLOSE)
    delay(90)
    addobject(18)
    clearflag(FLAG_HIDE_GRUNT_MULWOODS)
    applymovement(18, moves(walk_up * 5, delay_16 * 2, walk_in_place_down))
    waitmovement(0)
    msgbox(MulberryWoods_EventScript_GruntPreBattle1, MSGBOX_AUTOCLOSE)
    applymovement(18, moves(walk_left * 3, walk_up, walk_left, delay_16 * 2))
    waitmovement(0)    
    playse(SE_PIN)
    applymovement(18, moves(emote_question_mark))
    waitmovement(0)
    msgbox("Hm?\n" "...Wait a minute.", MSGBOX_AUTOCLOSE)
    playse(SE_PIN)
    applymovement(LOCALID_PLAYER, moves(emote_exclamation_mark, walk_in_place_right))
    applymovement(13, moves(emote_exclamation_mark, walk_in_place_right))
    applymovement(14, moves(emote_exclamation_mark))
    waitmovement(0)
    fadeoutbgm(0)
    delay(30)
    playbgm(MUS_HG_TEAM_ROCKET_HQ, FALSE)
    msgbox(MulberryWoods_EventScript_GruntPreBattle2, MSGBOX_AUTOCLOSE)
    delay(48)
    applymovement(LOCALID_PLAYER, moves(lock_facing_direction, walk_slow_left, unlock_facing_direction))
    applymovement(13, moves(delay_8, lock_facing_direction, walk_left, unlock_facing_direction))
    applymovement(14, moves(delay_16, lock_facing_direction, walk_slow_left, unlock_facing_direction))
    waitmovement(0)
    applymovement(18, moves(delay_16 * 2, walk_left))
    waitmovement(0)
    message(MulberryWoods_EventScript_GruntPreBattle3)
    waitmessage
    delay(30)
    dynmultichoice(0, 0, TRUE, 6, 0, DYN_MULTICHOICE_CB_NONE, "Hand over that package!", "(Try to imitate a Pokémon's cry, loudly.)")
    if(var(VAR_RESULT) == 0) {
        msgbox(MulberryWoods_EventScript_GruntHandPackage, MSGBOX_AUTOCLOSE)
    } else {
        closemessage
        delay(60)
        msgbox(MulberryWoods_EventScript_GruntConfused, MSGBOX_AUTOCLOSE)
    }
    delay(48)
    applymovement(18, moves(walk_in_place_right, delay_16 * 2, walk_in_place_left))
    waitmovement(0)
    msgbox(MulberryWoods_EventScript_GruntPreBattle4, MSGBOX_AUTOCLOSE)
    applymovement(13, moves(face_up))
    playmoncry(SPECIES_ZANGOOSE, CRY_MODE_NORMAL)
    waitmoncry
    playmoncry(SPECIES_SEVIPER, CRY_MODE_NORMAL)
    waitmoncry
    playse(SE_PIN)
    applymovement(LOCALID_PLAYER, moves(walk_in_place_down, emote_question_mark, delay_16, walk_in_place_left))
    waitmovement(LOCALID_PLAYER)
    delay(60)
    msgbox("(... ... ... ...Oh!)\p" "(These Pokemon...\n" "They want to join your side!)", MSGBOX_AUTOCLOSE)
    delay(90)
    playse(SE_BALL_OPEN)
    applymovement(14, moves(enter_pokeball))
    applymovement(13, moves(enter_pokeball))
    waitmovement(0)
    removeobject(13)
    removeobject(14)
    delay(90)
    setflag(FLAG_SYS_POKEMON_GET)
    givemon(SPECIES_ZANGOOSE, 5, nature=NATURE_JOLLY, hpIv=31, atkIv=31, defIv=31, speedIv=31, spAtkIv=31, spDefIv=31)
    givemon(SPECIES_SEVIPER, 5, nature=NATURE_IMPISH, hpIv=31, atkIv=31, defIv=31, speedIv=31, spAtkIv=31, spDefIv=31)
    message("Zangoose and Seviper have joined\n" "your party!")
    waitmessage
    playfanfare(MUS_HG_EVOLVED)
    waitfanfare
    closemessage
    fadeoutbgm(0)
    delay(90)
    applymovement(18, moves(walk_in_place_slow_left))
    waitmovement(0)
    msgbox(MulberryWoods_EventScript_GruntPreBattle5, MSGBOX_AUTOCLOSE)
    delay(48)
    playbgm(MUS_HG_ENCOUNTER_ROCKET, FALSE)
    playse(SE_LEDGE)
    applymovement(18, moves(jump_in_place_left, walk_in_place_fast_left * 10))
    applymovement(LOCALID_PLAYER, moves(walk_in_place_right))
    waitmovement(0)
    msgbox(MulberryWoods_EventScript_GruntPreBattle6, MSGBOX_AUTOCLOSE)
    setflag(FLAG_NO_WHITEOUT)
    trainerbattle_no_intro(TRAINER_GRUNT_MULBERRY_WOODS, MulberryWoods_Text_NoWay)
    goto(MulberryWoods_EventScript_GruntPostBattle)
    end
}

script MulberryWoods_EventScript_GruntPostBattle {
    applymovement(18, moves(lock_facing_direction, walk_slow_right, unlock_facing_direction))
    waitmovement(0)
    specialvar(VAR_RESULT, GetBattleOutcome)
    if(var(VAR_RESULT) == B_OUTCOME_LOST) {
        msgbox(MulberryWoods_EventScript_GruntPostBattleLost, MSGBOX_AUTOCLOSE)
    } else {
        msgbox(MulberryWoods_EventScript_GruntPostBattleText1, MSGBOX_AUTOCLOSE)
    }
    clearflag(FLAG_NO_WHITEOUT)
    delay(60)
    setspeaker(SP_NAME_SCARLETT)
    msgbox(MulberryWoods_EventScript_GruntPostBattleScarlett1, MSGBOX_AUTOCLOSE)
    playse(SE_PIN)
    applymovement(18, moves(emote_exclamation_mark, walk_in_place_right))
    waitmovement(0)
    msgbox(MulberryWoods_EventScript_GruntPostBattleText2, MSGBOX_AUTOCLOSE)
    applymovement(18, moves(walk_in_place_fast_left))
    waitmovement(0)
    msgbox(MulberryWoods_EventScript_GruntPostBattleText3, MSGBOX_AUTOCLOSE)
    applymovement(18, moves(walk_left * 2))
    waitmovement(0)
    //give player hawthorne's parcel item here once I make it
    delay(60)
    applymovement(18, moves(lock_facing_direction, walk_right, unlock_facing_direction))
    waitmovement(0)
    msgbox(MulberryWoods_EventScript_GruntPostBattleText4, MSGBOX_AUTOCLOSE)
    applymovement(18, moves(walk_fast_right * 4, walk_fast_down * 7))
    waitmovement(0)
    removeobject(18)
    delay(90)
    setspeaker(SP_NAME_SCARLETT)
    msgbox(MulberryWoods_EventScript_GruntPostBattleScarlett2, MSGBOX_AUTOCLOSE)
    delay(90)
    setobjectxyperm(9, 52, 27)
    clearflag(FLAG_HIDE_SCARLETT_MULWOODS1)
    addobject(9)
    applymovement(9, moves(walk_up * 5, walk_in_place_left, delay_16 * 3, walk_fast_left * 3, walk_fast_up, walk_fast_left * 2))
    waitmovement(0)
    setspeaker(SP_NAME_SCARLETT)
    msgbox(MulberryWoods_EventScript_GruntPostBattleScarlett3, MSGBOX_AUTOCLOSE)
    applymovement(9, moves(walk_left))
    waitmovement(0)
    fadescreenswapbuffers(FADE_TO_BLACK)
    special(HealPlayerParty)
    playfanfare(MUS_HEAL)
    waitfanfare
    delay(48)
    fadescreenswapbuffers(FADE_FROM_BLACK)
    applymovement(9, moves(lock_facing_direction, walk_right, unlock_facing_direction))
    waitmovement(0)
    setspeaker(SP_NAME_SCARLETT)
    msgbox(MulberryWoods_EventScript_GruntPostBattleScarlett4, MSGBOX_AUTOCLOSE)
    applymovement(9, moves(walk_right * 4, walk_down * 7))
    waitmovement(0)
    removeobject(9)
    delay(90)
    //clear flag for no wild battles here
    msgbox(MulberryWoods_EventScript_GruntPostBattleTagBreaks, MSGBOX_AUTOCLOSE)
    setvar(VAR_MULBERRY_TOWN_STATE, 2)
    setvar(VAR_HAWTH_LAB_STATE, 2)
    releaseall
}

script MulberryWoods_EventScript_NormalGirl {
    lock
    faceplayer
    msgbox(MulberryWoods_EventScript_NormalGirlText)
    release
}

script MulberryWoods_EventScript_PressBSign {
    lock
    msgbox(MulberryWoods_EventScript_PressBSignText)
    release
}

script MulberryWoods_EventScript_ExitSign {
    lock
    msgbox(MulberryWoods_EventScript_ExitSignText)
    release 
}

script MulberryWoods_EventScript_GoOtherWay {
    lock
    msgbox(MulberryWoods_EventScript_GoOtherWayText, MSGBOX_AUTOCLOSE)
    applymovement(LOCALID_PLAYER, moves(walk_right))
    waitmovement(LOCALID_PLAYER)
    release
}

text MulberryWoods_EventScript_ScarlettText1 {
    "Oh, {PLAYER}. There you are!\n"
    "Took you long enough!"
}

text MulberryWoods_EventScript_ScarlettText2 {
    "Alright, so. Here's the plan.\p"
    "Whoever stole those Pokémon couldn't\n"
    "have gotten very far."
}

text MulberryWoods_EventScript_ScarlettText3 {
    "I'll check this side of the woods, and\n"
    "ask the trainers here if they saw\l"
    "anyone suspicious run by."
}

text MulberryWoods_EventScript_ScarlettText4 {
    "Meanwhile, you'll cover this side of the\n"
    "woods.\p"
    "If you run into the thief, then...{PAUSE 80}\n"
    "... um...\p"
    "Right, you, uh, don't have any Pokémon\n"
    "yet, do you...\p"
    "Then...{PAUSE 60} uh....{PAUSE 72} try distracting them!\n"
    "Make a loud noise or something!\p"
    "I promise, I'll come running to help as\n"
    "soon as I hear it!\p"
    "... ... ... ... ... ... ...\p"
    "Don't give me that look, alright?!\n"
    "We don't have time to come up with\l"
    "a better plan!\p"
    "Listen, I know you'll be fine.\n"
    "I know we've only just met, but...\p"
    "You seem like the kind of person who\n"
    "can handle themselves in any situation.\p"
    "So... I know that things will turn out\n"
    "just fine.\p"
    "...Okay?{PAUSE 72} Okay!\p"
    "Let's go thief hunting!"
}

text MulberryWoods_EventScript_GruntPreBattle1 {
    "Whew, alright!\n"
    "I think I shook those people off.\p"
    "Man, hiding in those bushes is such a\n"
    "pain.\p"
    "Now to find those Pokémon and get this\n"
    "package to the boss fast..."
}

text MulberryWoods_EventScript_GruntPreBattle2 {
    "You there!\n"
    "Those Pokémon belong to me!\l"
    "I stole them fair and square.\p"
    "Now, get out of the way so I can take\n"
    "them back!"
}

text MulberryWoods_EventScript_GruntPreBattle3 {
    "Hey, don't try anything funny!\n"
    "Look around, there's nowhere else\l"
    "for you to run, you know!"
}

text MulberryWoods_EventScript_GruntHandPackage {
    "Oh, you're one of the people looking for\n"
    "this package, huh?\p"
    "Well, that's too bad!\p"
    "The Pokémon in here belong to my\n"
    "crew now!"
}

text MulberryWoods_EventScript_GruntConfused {
    "... ... ...Huh?\p"
    "W{PAUSE 60}-What was that supposed to be?\n"
    "What was that noise you just made?!"
}

text MulberryWoods_EventScript_GruntPreBattle4 {
    "L{PAUSE 60}-Look, I don't have time for this!\p"
    "I've gotta get out of here and get this\n"
    "package over to my boss.\p"
    "So you'd better move out of the way and\n"
    "let me get those Pokémon, or else I'll-"
}

text MulberryWoods_EventScript_GruntPreBattle5  {
    "... ... ... ... ... ... ... ... ... ... ... ... ... ... ... ... ...\p"
    "... ... ... ...{PAUSE 90}What?\p"
    "Did you-\n"
    "Did you just..."
}

text MulberryWoods_EventScript_GruntPreBattle6 {
    "Did you just take those Pokémon\n"
    "WHILE I WAS TALKING TO YOU?!\p"
    "Oh, that's it!\n"
    "I've had enough!\p"
    "I'll show you what happens to people\n"
    "who mess with Team Alpha!"
}

text MulberryWoods_Text_NoWay {
    "N-no way! I lost?!\n"
    "Why, you little..."
}

text MulberryWoods_EventScript_GruntPostBattleLost {
    "Heh... heheh...\n"
    "I... I actually won?!\p"
    "Ha! Well, you can keep those Pokémon!\n"
    "They're clearly not worth my time!"
}

text MulberryWoods_EventScript_GruntPostBattleText1 {
    "You've gotta be kidding me...\p"
    "Fine, keep those Pokémon!\n"
    "I didn't even like them anyway!"
}

text MulberryWoods_EventScript_GruntPostBattleScarlett1 {
    "{PLAYER}! Are you okay?\n"
    "I heard a lot of noise coming from\l"
    "over here.\p"
    "Don't worry, I'm on my way!"
}

text MulberryWoods_EventScript_GruntPostBattleText2 {
    "Oh no!\n"
    "I can't deal with another person\l"
    "right now!"
}

text MulberryWoods_EventScript_GruntPostBattleText3 {
    "Urgh, stealing this package has been\n"
    "way too stressful!\p"
    "Whatever, just take it!\n"
    "I'm outta here!"
}

text MulberryWoods_EventScript_GruntPostBattleText4 {
    "A{PAUSE 60}-Anyway, don't get cocky!\p"
    "You may have won this time, but mark\n"
    "my words...\p"
    "You'll regret meddling with Team Alpha!"
}

text MulberryWoods_EventScript_GruntPostBattleScarlett2 {
    "Oof!{PAUSE 90} Hey!\n"
    "Where do you think you're going?!"
}

text MulberryWoods_EventScript_GruntPostBattleScarlett3 {
    "{PLAYER}! What happened?!\p"
    "That... that was the thief, wasn't it?\n"
    "What did they do? Are you hurt?\p"
    "... ... ... ... ... ... ... ... ... ... ... ...{PAUSE 72}\n"
    "Hold on, you got the package!\p"
    "And those Poké Balls at your side...\n"
    "When did you...?\p"
    "Er, let me heal your Pokémon first.\n"
    "Then you can explain everything."
}

text MulberryWoods_EventScript_GruntPostBattleScarlett4 {
    "Huh... so that's what happened.\p"
    "I'm sorry I wasn't there to help, but\n"
    "you handled things really well on\l"
    "your own!\p"
    "... ... ... ...{PAUSE 72}Ah, right!\n"
    "We need to get that package back\l"
    "to the professor.\p"
    "I bet he's worried sick about us by now.\n"
    "I'll meet you back at the lab, okay?\p"
    "Now that you have Pokémon, you won't\n"
    "have much trouble making it back.\p"
    "Alright, see you there!"
}

text MulberryWoods_EventScript_GruntPostBattleTagBreaks {
    "(... ... ... ... ... ...{PAUSE 72}!)\p"
    "(The tag Prof. Hawthorne gave you must\n"
    "have fallen off during the battle...)"
}

text MulberryWoods_EventScript_NormalGirlText {
    "Hm? Oh no, I'm not a trainer.\p"
    "But there are quite a few of them\n"
    "between here and Bisque City, so you\l"
    "should make sure you're stocked up on\l"
    "supplies."
}

text MulberryWoods_EventScript_PressBSignText {
    "Trainer Tips!\p"
    "When facing a wild Pokémon you don't\n"
    "feel like fighting, press B to quickly\l"
    "move your cursor to the “Run” option!"
}

text MulberryWoods_EventScript_ExitSignText {
    "Leaving Mulberry Woods\n"
    "Bisque City Ahead"
}

text MulberryWoods_EventScript_GoOtherWayText {
    "(There are Pokémon trainers past here.\n"
    "Best to go the other way for now.)"
}
